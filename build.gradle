apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'

version = '0.9.3'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
    mavenLocal()
}

configurations {
    provided
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
}


dependencies {

    //lucene dependencies
    compile group: 'org.apache.lucene', name: 'lucene-core', version: '4.8.1'
    compile group: 'org.apache.lucene', name: 'lucene-analyzers-common', version: '4.8.1'
    compile('org.apache.lucene:lucene-queryparser:4.8.1') {
        exclude group: 'org.apache.lucene', module: 'lucene-sandbox'
    }
    compile('org.apache.lucene:lucene-join:4.8.1')

    compile('joda-time:joda-time:2.3')

    //additional libraries
    compile('org.codehaus.jackson:jackson-mapper-asl:1.9.13')
    compile('org.codehaus.jackson:jackson-core-asl:1.9.13')

    compile group: 'commons-collections', name: 'commons-collections', version: '3.2.1'

    provided group: 'com.google.guava', name: 'guava', version: '15.0'

    provided('org.apache.commons:commons-lang3:3.1')

    //provided dependencies
    provided('org.apache.cassandra:cassandra-all:2.0.9') {
        exclude group: 'asm'
        exclude group: 'org.apache.mina'
        exclude group: 'com.google.guava'
    }

    compile('org.slf4j:slf4j-api:1.7.2')
    compile('org.slf4j:slf4j-log4j12:1.7.2')

    //testing dependencies
    testCompile group: 'junit', name: 'junit', version: '4.11+'

    testCompile('com.datastax.cassandra:cassandra-driver-core:2.0.1') {
        exclude group: 'org.apache.cassandra', module: 'cassandra-all'
        exclude group: 'log4j'
        exclude group: 'io.netty'
        exclude group: 'com.google.guava'
    }

    testCompile('org.cassandraunit:cassandra-unit:2.0.2.0') {
        exclude group: 'org.apache.cassandra', module: 'cassandra-all'
        exclude group: 'log4j'
        exclude group: 'org.sl4j'
        exclude group: 'org.jboss.netty'
        exclude group: 'com.google.guava'
    }

    testCompile group: 'net.sourceforge.argo', name: 'argo', version: '3.7'

}

test {
    jvmArgs '-javaagent:ext/jamm-0.2.5.jar'
}

task perfTest(type: Test) << {
    jvmArgs '-javaagent:ext/jamm-0.2.5.jar'
    include 'com/tuplejump/perf/**'
}

idea {
    module {
        scopes.PROVIDED.plus += configurations.provided
    }
}

eclipse {
    classpath {
        plusConfigurations += configurations.provided
    }
}

task copyLibs(type: Copy) {
    from configurations.compile
    into 'build/libs'
}

jar { dependsOn copyLibs }

task dist(type: Zip) {
    dependsOn jar
    from('build/libs/')
    include '*.*'
}

task distTar(type: Tar) {
    dependsOn jar
    from('build/libs/')
    include '*.*'
}

install {
    repositories.mavenInstaller {
        pom.version = '0.9.3'
        pom.groupId = 'com.tuplejump'
        pom.artifactId = 'stargate-core'
    }
}
